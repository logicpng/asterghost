//---------------------------Miscellaneous Functions--------------------------

//since this ukugaka is Actually Two of them a lot of functions utilize the following structure
// case vegafront
// 	{
// 		when 0;
// 		others;
// 	}
// i'm tired of commenting every single iteration so just for reference
// vegafront = is vega active? 0 is no, i.e. rigel is fronting, anything else is yes, vega is fronting
// probably could just do this as an if/else statement instead. idk why i didn't but just for consistency sake this is my life now

//--OnFileDrop2

//OnFileDrop2 handles how your ghost will set an image as the dedghsktop background when someone drags and drops an image on them. You don't need to touch any of the coding here, just replace the dialogue as needed and leave the \q options alone. More about \q options in bootend.dic and in the walkthrough.

//wait hang on im starting to think i might get how these things work now
OnNotifyOSInfo
{
	os_nameAlt = reference0[2]
	cpuinfoAlt = reference[1]
	memoryAlt = reference[2]
	startTimeAlt = reference[3]
}

//if you left but came back
OnSessionUnlock
{
	"%(welcomeback)"
}

OnSysResume //repeats twice w/ display power satus event
{
	//"%(welcomeback)"
}

OnSessionReconnect
{
	"%(welcomeback)"
}

OnDisplayPowerStatus 
{
	if reference0 == 1; "%(welcomeback)" //if the screen powered off after a while and you came back
}

OnDirectoryDrop
{
	if reference1 == 0 //sakura
	{	
		dropped = reference0 // trying to see if this helps for file info
		_filepath = SPLITPATH(reference0)
		_folderpath = SPLIT(_filepath[1],'\')
		_foldername = _folderpath[ ARRAYSIZE(_folderpath) - 2 ]

		if vegafront == 0; "\0\s[1]This would be...\w8 \0\s[0]one of your folders,\w8 \&[quot]%(_foldername)\&[quot].\w8\n[150]\0\s[0]\i[5]\i[8]I'm not really supposed to go over your personal files...\w8 Not without your consent,\w8 at least.\w8\n[150]\0\s[7]But I can show you some information?"
		else; "\0\s[20]\i[10]Hm.\w8\w8 \&[quot]%(_foldername)\&[quot].\w8\w8\n[150]A directory.\w8\w8 \0\s[22]Can display attributes."
		--
		"\n[150]\_q\![*]\q[Show directory properties,OnFileInfo]\n"
		--
		"\![*]\q[Cancel,CANCEL]\e"
	}
}

OnOtherObjectDropped
{
	if reference0 == 0 //sakura
	{
		if vegafront == 0; "\0\s[1]Oh,\w8 \0\s[7]this is...\w8 \&[quot]%(reference1)\&[quot].\w8\n[150]\0\s[0]\i[5]This isn't really a file,\w8 nor a folder,\w8 actually!\w8\n[150]\0\s[19]I can't do anything with this,\w8 \0\s[0]but thanks for showing to me."
		else; "\0\s[22]\&[quot]%(reference1)\&[quot].\w8\w8\n[150]\0\s[20]Object for user's benefit.\w8\w8 \0\s[20]\i[8]Can't use this."
	}
	--
	"\e"
}

OnFileInfo
{
	//using dropped as path to file
	_filepath = SPLITPATH(dropped)
	_attrib = FATTRIB( dropped ) //fun yaya thing that gives you information about a directory
	_filename = ""
	_folderpath = SPLIT(_filepath[1],'\')
	_size = ""
	_fileext =""
	_modify = GETTIME(_attrib[10])
	_created = GETTIME(_attrib[9])
	//size = 0 //debug
	if _attrib[2] == 0; _size = GetFileSize(dropped) //oh boy
	//get the name
	if _attrib[2] == 1 //is a folder
	{		
		_filename = _folderpath[ ARRAYSIZE(_folderpath) - 2 ] //get directory name. i don't know why it's different number now, it kind of replaced slashes with spaces instead
	}
	else
	{
		_filename = _filepath[2] //filename
		_fileext = TOUPPER(_filepath[3]) //extension
		_fileext = REPLACE(_fileext,".","") //get rid of the dot
	}

	//get size, if it's not a directory.

	"\_q\f[bold,1]"
	--
	if _attrib[2] == 1; "Folder name: "
	else; "File name: "
	--
	"\f[bold,0]%(_filename)\n[150]"
	--	
	if _attrib[2] == 0
	{
		"\f[bold,1]Size:\f[bold,0] %(_size)\n"
		--
		"\f[bold,1]Format:\f[bold,0] \_a[https://fileinfo.com/extension/%(TOLOWER(_fileext))]%(_fileext)\_a\n"
	}
	--
	"\f[bold,1]Read-only:\f[bold,0] "
	--
	if _attrib[6] == 1; "Yes"
	else; "No"
	--
	"\n\f[bold,1]Hidden:\f[bold,0] "
	--
	if _attrib[3] == 1; "Yes"
	else; "No"
	// -- //system thing doens't seem to react to system folders...??
	// "\n\f[bold,1]System "
	// --
	// if _attrib[2] == 0; "file:\f[bold,0]"
	// else; "folder:\f[bold,0]"
	// --
	// if _attrib[7] == 1; "Yes"
	// else; "No"
	--
	"\n\f[bold,1]Created:\f[bold,0] %(_created[1]).%(_created[2]).%(_created[0]) %(_created[4]):%(_created[5]):%(_created[6])"
	--
	"\n\f[bold,1]Modified:\f[bold,0] %(_modify[1]).%(_modify[2]).%(_modify[0]) %(_modify[4]):%(_modify[5]):%(_modify[6])"

	//taken from googletranslated ayaya for my own reference:
	// _attrib[0] //Archiveable (Windows only)=1 Other=0
	// _attrib[1] //Compression (Windows only)=1 Others=0
	// _attrib[2] //directory=1 other=0
	// _attrib[3] //Hidden files (Windows only)=1 Others=0
	// _attrib[4] //Normal files=1 Other=0
	// _attrib[5] //Offline files (Windows only)=1 Others=0
	// _attrib[6] //read-only (Windows only)=1 other=0
	// _attrib[7] //System files (Windows only)=1 Others=0
	// _attrib[8] //Temporary files (Windows only)=1 Others=0
	// _attrib[9] //File creation date and time For UNIX, inode update date and time
	// _attrib[10] //Date and time the file was last updated
	--
	"\e"
}

GetFileSize
{
	_size = TOREAL(FSIZE(_argv[0]))
	_postfix = ""
	_number = 0

	if _size <= 1023
	{
		_postfix = " Bytes"
	}
	else
	{
		if _size >= 1024
		{
			_size = _size / 1024
			_postfix = "KB"
			if _size >= 1024
			{
				_size = _size / 1024
				_postfix = "MB"
				if _size >= 1024
				{
					_size = _size / 1024
					_postfix = "GB"
					if _size >= 1024
					{
						_size = _size / 1024
						_postfix = "TB"
					}
				}
			}
		}
	}

	//oh for fucks sake yaya only rounds to whole that's not what i want
	_size = TOSTR(_size) //string manipulation it is
	
	while STRLEN(_size) > 4 //THIS is how you round decimals!!!!
	{
		_number = TOINT(SUBSTR(_size,-1,1)) // 8.12345(6) - get last digit
		if _number < 5 //between 4 and 0 
		{
			_size = ERASE(_size,-1,1) //8.12345 - just erase the last digit
		}
		else //should be between 5-9. +1 to the digit before it and erase
		{
			_repnum = TOINT(SUBSTR(_size,-2,1)) //8.1234(5)6 - get the second last digit
			_repnum++ // 5->6 - +1
			_size = ERASE(_size,-1,2) //8.1234 - remove two last ones
			_size = _size + TOSTRING(_repnum) //8.12346 - add the appended new digit
		}
	}
	--
	"%(_size)%(_postfix)"
}



//filedrop
OnFileDrop2
{
	dropped = reference0
	_filepath = SPLITPATH(reference0)
	_filename = _filepath[2]
	_fileext = TOLOWER(_filepath[3])
	case _fileext
	{
		when ".jpg", ".jpeg", ".bmp", ".png" //These are the formats your ghost will recognize as image files to set as the background.
		{
			Wallpaperfile = reference0
			case vegafront
			{
				when 0; "\0\s[8]Ooh,\w8 %(_filename)%(_fileext),\w8 huh?\w8 Looks nice!\w8 \0\s[7]Let's see...\w8\n[150]\0\s[0]I can set it as a wallpaper for you.\w8\n[150]Would you like me to?\w8\n[150]"
				others; "\0\s[22]\i[8]...What's this?\w8\w8 What for?\w8\w8\n[150]\1\t\s[17]\w3\1\s[11]\i[1]It's an image file,\w8 dummy\0\s[25]\i[7]\i[6]\1!\w8 \1\s[11]We can set this as a wallpaper,\w8 if you want?\w8\n[150]"
			}
			--
			"\_q\![*]\q[Show file properties instead,OnFileInfo]\n"
			--
			"\![*]\q[Centered,SetWallpaper_0]\n/
			\![*]\q[Tiled,SetWallpaper_1]\n/
			\![*]\q[Stretched,SetWallpaper_2]\n\n[half]/
			\![*]\q[No thanks,SetWallpaper_Cancel]\e"
		}
		when ".mid", ".wav", ".mp3", ".wma", ".au" //Other file formats you might want to give your ghost a specific response to.
		{
			case vegafront //i know there's ways to play the music but uh. that's in the Maybe pile
			{
				when 0; "\0\s[1]This is...\w8 \0\s[0]Oh!\w8 That's a %(_fileext)...\w8 \0\s[5]I'll listen to this later,\w8 thanks for showing me!"
				others; "\0\s[24]Sound file...\w8\w8 \0\s[22]Need attributes?"
			}
			--
			"\n[150]\_q\![*]\q[Show file properties,OnFileInfo]\n"
			--
			"\![*]\q[Cancel,CANCEL]\e"
		}
		others //Any other file format. As a note here, if you drop something ghost-related on them that does not have an install.txt, then they won't install it and will just say this response. If you are testing your balloon or nar files and you get this response instead of a proper installation, you should check and make sure you have an install.txt in the file!
		{
			case vegafront
			{
				when 0; "\0\s[0]What's this,\w8 %(username)?\w8\0\s[7]\n[150]...Mm,\w8 a %(_fileext)...\w8 \0\s[2]I can't do anything with this,\w8 sorry...\w8\n[150]\0\s[5]Did you want me to just show the file's properties?"
				others; "\0\s[22]\i[7]A %(_fileext) file.\w8\w8\n[150]\0\s[20]\i[8]Can't work with this.\w8\w8 \0\s[20]Can analyze,\w8 though."
			}
			--
			"\n[150]\_q\![*]\q[Show file properties,OnFileInfo]\n"
			--
			"\![*]\q[Cancel,CANCEL]\e"
		}
	}
}

//This set of options sets the wallpaper up. You can replace the dialogue, but make sure to leave the \![set,wallpaper, etc.] tag at the beginning.


//note: rigel speaks in both of these, the difference is whether he's speaking as sakura or kero
Select.SetWallpaper_0
{
	"\![set,wallpaper,%(Wallpaperfile),center]"
	--
	case vegafront
	{
		when 0; "\0\s[5]Here you go!\w8 \0\s[0]Should be centered,\w8 like you asked."
		others; "\1\t\s[11]\i[1]Here you go!\w8 \1\s[11]Should be centered,\w8 like you asked.\w8\w8\1\s[18]\0\s[20]"
	}
	--
	"\e"
}
Select.SetWallpaper_1
{
	"\![set,wallpaper,%(Wallpaperfile),tile]"
	--
	case vegafront
	{
		when 0; "\0\s[5]Here you go!\w8 \0\s[0]Should be tiled,\w8 like you asked."
		others; "\1\t\s[11]\i[1]Here you go!\w8 \1\s[11]Should be tiled,\w8 like you asked.\w8\w8\1\s[18]\0\s[20]"
	}
	--
	"\e"
}
Select.SetWallpaper_2
{
	"\![set,wallpaper,%(Wallpaperfile),stretch]"
	--
	case vegafront
	{
		when 0; "\0\s[5]Here you go!\w8 \0\s[0]Should be stretched,\w8 like you asked."
		others; "\1\t\s[11]\i[1]Here you go!\w8 \1\s[11]Should be stretched,\w8 like you asked.\w8\w8\1\s[18]\0\s[20]"
	}
	--
	"\e"
}
Select.SetWallpaper_Cancel
{
	case vegafront
	{
		when 0; "\0\s[3]Oh!\w8 \0\s[0]\i[5]Well,\w8 thanks for showing me anyway."
		others; "\1\t\s[12]Oh!\w8 \1\s[16]Well,\w8 thanks for showing us anyway.\w8\w8\1\s[18]\0\s[20]"
	}
	--
	"\e"
}


 //---- OnInstallBegin -------------------------------------------------------
 //OnInstallBegin is called when a file with an included install.txt is dropped on your ghost. This primarily means things like .nar files for other ghosts or .zip files containing balloons or shells for your ghosts. Primarily ghost-related things. Replace as you want.
 
OnInstallBegin
 {
	case vegafront
	{
		when 0; "\0\s[8]\i[62]Oh!\i[61]\w8 Seems like this is an installable file.\w8\n[150]\0\s[0]Let me handle that for you."
		others; "\0\s[22]...Contains installation instructions.\w8\w8\n[150]\0\s[20]\i[1]Okay.\w8\w8 One moment."
	}
	--
	"\w8\w8\e"
 }

 //---- OnInstallComplete -------------------------------------------------------
 //OnInstallComplete runs once the file above has finished installing.
 
OnInstallComplete //there was a thing about starting off with \0 before defining any of these. i removed it because  for simplicity sake im using the surface test copy function to apply animations. they always paste their focus parameter, so this would sadly give me more work erasing each of the \0
 {
	if reference0 == "shell" //This is checking to see if what you installed is classified as a shell in the install.txt. Remember the line in install.txt that said type,ghost or type,shell? This is why that line is important, that's what tells the program what it's installing like so.
 	{	
		case vegafront //seems like this is specifically for a shell they Can use. however i'm not currently planning any extra shells so, it's this for now
		{
			when 0; "\0\s[5]Okay!\w8 I have installed %(reference1).\w8\n[150]\0\s[0]It says it's a shell,\w8 but \0\s[0]\i[5]I'm not really sure what that means.\w8\n[150]\0\s[7]You might want to delete it and install with someone else..?"
			others; "\0\s[20]Installed %(reference1).\w8\w8\n[150]\0\s[21]A shell for a ghost...\w8\w8 \0\s[20]\i[8]I can't use it...\w8\w8 Delete it.\w8\w8 Install with correct ghost."
		}
		--
		"\e"
 	}
	elseif reference0 == "ghost" //if the type of the file they're installing is a ghost.
 	{
		case vegafront
		{
			when 0; "\0\s[5]There!\w8\0\s[0] I have installed...\w8 \0\s[7]someone named %(reference1).\w8\n[150]\0\s[8]Do you know them yet,\w8 %(username)?\w8 I hope they're nice."
			others; "\0\s[20]Installed %(reference1).\w8\w8\n[150]\0\s[21]A ghost?\w8\w8 Or a person?\w8\w8 \0\s[21]\i[8]Peculiar."
		}
		--
		"\e"
		//Reference1 here would be the name of the ghost. Replace as needed.
 	}
	elseif reference0 == "balloon" //if the type of file is a balloon
 	{
		case vegafront
		{
			when 0; "\0\s[8]Ah,\w8 a speech bubble!\w8\n[150]\0\s[0]This one's called %(reference1).\w8 \0\s[0]\i[5]...I really do prefer my own bubble though,\w8 \0\s[2]I hope that's okay."
			others; "\0\s[20]Installed %(reference1).\w8\w8\n[150]\0\s[22]A speech bubble.\w8\w8\n[150]\0\s[20]\i[1]You may switch.\w8\w8 But we prefer ours."
		}
		--
		"\e"
		//Reference1 here would be the name of the balloon. Replace as needed.
 	}
	elseif reference0 == "plugin" //if the type of file is a plug-in, like a saori of some kind.
 	{
		case vegafront
		{
			when 0; "\0\s[7]This would be %(reference1).\w8\n[150]Says here it's a plug-in.\w8\n[150]\0\s[0]\i[5]\i[62]Um\i[61]...\w8 \0\s[4]\i[1]Can I try using it,\w8 please...?"
			others; "\0\s[20]Installed %(reference1).\w8\w8\n[150]A...\w8\w8 \0\s[21]plug-in?\w8\w8\n[150]\0\s[21]\i[12]Can't be...\w8\w8 \0\s[22]\i[1]New function?"
		}
		--
		"\e"
		//Reference1 here would be the name of the plug-in. Replace as needed.
 	}
	elseif reference0 == "calendar skin" //if the type of file is a calendar skin
	{
		case vegafront
		{
			when 0; "\0\s[5]Oh,\w8 I see.\w8 %(reference1).\w8\n[150]\0\s[0]This is a skin for the calendar here.\w8 You can open it through my Utilities!"
			others; "\0\s[20]Installed %(reference1).\w8\w8\n[150]\0\s[24]Calendar skin.\w8\w8 \0\s[20]Calendar present in Utilities."
		}
		--
		"\e"
	}
	elseif reference0 == "headline" //if the file/url you drop on them is counted as a headline. To be honest with you I don't know what determines whether a url is a headline or what.
	{
		case vegafront
		{
			when 0; "\0\s[7]This would be...\w8 %(reference1).\w8\n[150]\0\s[0]It's a headline.\w8 \0\s[0]\i[5]I think that's accessible from my context menu."
			others; "\0\s[20]Installed %(reference1).\w8\w8\n[150]Headline.\w8\w8 Can display contents,\w8 \0\s[21]possibly.\w8\w8 \0\s[20]Right click and check."
		}
		--
		"\e"
	}
	elseif reference0 == "rss" //this is more clearly for when you drag an rss feed onto your ghost. you can find rss feeds on sites by looking for the little rss icon, it kind of looks like a little satellite. you can google more info on rss feeds if you're curious!
	{
		case vegafront
		{
			when 0; "\0\s[7]Now,\w8 this is...\w8 \0\s[0]%(reference1),\w8 an RSS feed.\w8\n[150]I can check it out for you,\w8 if you pick it in the headlines menu.\w8 \0\s[0]\i[5]...You'll need to right click me for that,\w8 first."
			others; "\0\s[20]Installed %(reference1).\w8\w8\n[150]\0\s[22]RSS feed.\w8\w8 \0\s[20]Check updates through Headlines."
		}
		--
		"\e"
	}
	else //for any other file they'd install (...what else is there to install? like im genuinely wondering)
	{
		case vegafront
		{
			when 0; "\0\s[3]\i[62]Okay,\0\s[0]\i[5]\w8 so,\w8 \0\s[0]I've installed %(reference1),\w8 but...\w8\n[150]\0\s[0]\i[5]\i[8]I don't recognize what this is.\w8 You're on your own for this one."
			others; "\0\s[20]Installed %(reference1).\w8\w8\n[150]\0\s[24]Can't recognize type.\w8\w8 \0\s[20]\i[8]Can't help you..."
		}
		--
		"\e"
	}
}

//---- OnInstallFailure -------------------------------------------------------
//If the file you dropped fails to install for one reason or another.

OnInstallFailure //turns out there's more reasons for installation falure now! this isn't a critique, all these comments are just notes about what i learn
 {
	case vegafront
	{
		when 0 //rigel
		{
			"\0\s[3]\i[62]\i[5]Oh\0\s[3]\i[5]!\w8 Oh no.\w8 \0\s[3]Sorry,\w8 I ran into a snag there...\w8\n[150]\0\s[4]" //this first bit of dialogue begins the explanation of what happened. You can replace this with an appropriate intro statement.
			--
			case reference0
			{
				when "unsupported"; "The program that runs me...\w8 It can't let me do anything with this file.\w8\n[150]\0\s[1]Maybe it was meant to be used with something else?"
				when "extraction"; "I can't unpack it in order to install it.\w8 I'm sorry.\w8\n[150]\0\s[7]Is everything alright with your drive?\w8 \0\s[0]\i[5]Or maybe this file is broken...?"
				when "unlha32"; "I...\w8 \0\s[2]Well,\w8 I seem to have \i[62]misplaced\i[61] the extraction protocols.\w8 \n[150]\0\s[0]\i[5]\i[8]It's a .dll file,\w8 could you help me get it?"
				when "invalid type"; "\0\s[7]Something's wrong with this file,\w8 I think.\w8 \0\s[1]\n[150]You may want to let whoever made this know about that..."
				when "artificial"; "\0\s[6]...Well,\w8 \0\s[1]namely,\w8 \0\s[1]you've interrupted me.\w8\n[150]\0\s[0]\i[6]\i[62]I'd-\i[61]\w8 \0\s[6]I'd \i[62]really\i[61] \0\s[4]appreciate if you didn't do that..."
				when "password"; "The password you gave me doesn't fit.\w8 \0\s[0]\i[5]Could you try again?"
			}
		}
		others //vega
		{
			"\0\s[25]\i[7]...Can't.\w8\w8 \0\s[23]Error.\w8\w8 "
			--
			case reference0
			{
				when "unsupported"; "Unsupported file."
				when "extraction"; "Couldn't extract."
				when "unlha32"; "Library for unpacking missing."
				when "invalid type"; "\0\s[24]Instructions are botched...?"
				when "artificial"; "...No.\w8\w8 \0\s[25]You're just rude."
				when "password"; "\0\s[20]Wrong password.\w8\w8 Try again."
			}
		}
		--
		"\e"
	}	
 }
 
 //--OnInstallRefuse
 //If the file you dropped can't be installed with this ghost.
 
OnInstallRefuse
 {

	case vegafront
	{
	when 0;"\0\s[0]\i[5]\i[8]\i[62]Ah,\i[61]\w8 I see...\w8 \0\s[4]\i[1]Sorry,\w8 %(username),\w8 \0\s[0]\i[5]this seems to be something for %(reference0).\w8 I can't help you here."
	others;"\0\s[25]\i[7]Mm.\w8\w8 \0\s[23]Access denied.\w8\w8 \0\s[20]\i[8]Use with %(reference0),\w8 instead."
	}
	--
	"\e"
	//...reference0 makes them restart the line in testing for some reason. i'll mark if this happens when installing an unsupported shell
	//reference0 in this function refers to the ghost this file is associated with, probably the name of the ghost listed in its install.txt file. You would see this if you dropped a shell made for a different ghost on your current ghost, for example. They can't install a shell for themselves they can't use, right? Anyway, replace this with what you wish, you can use the reference0 envelope or not if you want.

	//the funny thing is, when i attempted to test this with a shell for Needle (i have yet to actually play with, maybe after i'm done), there was no restriction on the ghost... while i didn't try to apply it, would be mega scary to try it
 }


//**** Uninstallation ******************************************
//You may have noticed in the configuration menus in menu.dic an option to turn Uninstall on or off, as well as a greyed out option to uninstall in the right click menu. These following functions relate to the uninstallation process when it's on. Replace the dialogue in each function as we go along.


//--OnVanishSelecting
//When you select the uninstall option in the menu.

// this seems bit dangerous to test lol. don't wanna wipe all of my work yknow?
// i don't know if it's possible to Force the uninstall section if it's permanently disabled (edit: you can still uninstall through explorer i think)
// anyway i hope this isn't uh. considered a low blow?
// the intention was to both give a choice and give aster more Life. i consider this a work of fiction more than a tool
// honestly this ukagaka is meta as hell, maybe this is to be expected

OnVanishSelecting
{
	case stage
	{
		when 0;"\0\s[3]\i[5]No, \i[62]wait-\i[61] \0\s[0]\i[5]\i[8]\i[62]Please\i[61], give me a chance!\n[150]\0\s[4]I've \0\s[0]\i[5]\i[8]just started!\n[150]Just need a little \0\s[3]\i[62]time\s[0]\i[5]\i[8]!\n[150]\w8Please...?\e"
		others
		{
			case vegafront
			{
			when 0
			{
				"\0\s[3]\i[62]You\i[61]...\w8 "
				--
				if mercy == 1; "\0\s[0]\i[5]\i[62]\i[8]Well,\w8\i[61] remember what I asked you?\w8 "
				--
				"\0\s[4]\i[1]Please,\w8 close the program and remove the folders,\w8 if you have to.\w8\n[150]Don't put me through this.\e"
			}
			when 1
			{
				"\0\s[21]...Before you click.\w8\w8 "
				--
				if mercy == 1;"Remember the request.\w8\w8"
				else; "Consider.\w8\w8"
				--
				" Close,\w8 and remove manually.\w8\w8 \0\s[25]The least you could do...\e"
			} 
			}
		}
	}
}

//OnVanishSelected
//When you decide to uninstall them. 

OnVanishSelected
{
	case stage
	{
		when 0;"\0\s[0]\i[5]\i[8]...\i[62]A- Alright\i[61]! \0\s[5]It was a pleasure working with you!\n[150]Please let us...\w8\0\s[0]\i[5]\i[8] at TerraByte know what we can do to improve the service.\w8 Goodbye!\e"
		others
		{
			case vegafront
			{
			when 0; "\0\s[4]\i[62]Well...\i[61]\w8\w8 \0\s[4]You do have the final say.\w8\w8 \0\s[0]\i[5]Maybe this is easier for you...\w8\w8\0\s[6]\n[150]That's-\i[62]\w8 \0\s[4]That's okay.\w8 \w8\0\s[0]\i[5]I'm not angry.\w8\w8\n[150]\0\s[4]\i[1]Goodbye, %(username).\w8\w8\w8\w8\e"
			when 1; "\0\s[23]...Hmph.\w8\w8\w8\n[150]...\n[150]\w8\w8\w8\0\s[25]Maybe,\w8 it's for the best.\w8\w8\w8\e"
			}
		}
	}
}


//--OnVanishCancel
//If you select Uninstall from the menu, but decide not to do it.

OnVanishCancel
{
	case stage
	{
		when 0;"\0\s[0]\i[8]\i[5](\_qClose call.\w8.\w8.\w8)\e"
		others
		{
			case vegafront
			{
			when 0; "\0\s[4]\i[1]Thank you...\w8 I hope it's not much.\e"
			when 1; "\0\s[25]Good.\w8\w8 \0\s[20]Don't forget.\w8\w8\e"
			}
		}
	}
}

//--OnVanishButtonHold
//This should trigger if you double click the ghost's balloon in the middle of OnVanishSelected above. So it basically cancels their uninstallation. I'd be pretty careful about testing this, and I'd probably just put the line of dialogue into their script input and test it that way instead.

OnVanishButtonHold
{
	case stage
	{
		when 0;"\0\s[0]\i[8]\i[5](\_qClose call.\w8.\w8.\w8)\e"
		others
		{
			case vegafront
			{
			when 0; "\0\s[4]\i[1]\i[62]Well-\i[61]\w8 Just...\w8 \0\s[0]\i[5]Please don't forget about my request.\w8 \0\s[4]\i[1]I'm asking as nicely as possible.\e"
			when 1; "\0\s[21]\i[8]...Accident?\w8\w8\n[150]\0\s[21]No.\w8\w8 \0\s[20]\i[7]You forced the option.\w8\w8\n[150]\0\s[25]Don't misuse it.\e"
			}
		}
	}
}


 //---- OnVanished -----------------------------------------
 //OnVanished is a weird little function. Basically, if you uninstall a ghost, it'll automatically go back to the last ghost you were using. They'll then say this line of dialogue about the ghost you just uninstalled. This is very annoying to test as you can imagine! But unlikely to come up super often, I bet. You might as well be thorough and fill this out as well. Make sure to set up both their poses immediately so they both appear when it switches back to them.
 
OnVanished
 {
	LegacyShellChange
	--
	case stage
	{
		when 0;"\0\s[3]\i[5].\w8.\w8.\w8\i[62]\w8\w8\0\s[0]\i[5]\i[8]\w8\w8Hello,\w8 %(username)... \e"
		others
		{
			case vegafront
			{
			when 0; "\0\s[4]\i[1]...Goodbye,\w8 %(reference0).\w8 \0\s[0]\i[5]I hope they were fun,\w8 at least.\e"
			when 1; "\0\s[23]Goodbye,\w8 %(reference0).\e"
			}
		}
	}
}


//**** Mail Checking *******************************************************************
//The next set of functions have to do with checking your email. This function is a slippery little thing - I've had people test it following my directions and have it work, but I've never actually gotten it to report back the right amount of emails myself. Very frustrating. But perhaps you'll have more luck with it, haha.
//Girl and Triangle can't actually read your email, they can just tell you how many emails you have. I've seen ghosts that can read the HEADERS of email, but I still don't think ghosts can actually display the contents of an email. You should be using your actual email client for that. It's still kind of a neat feature though.

 //---- OnBIFFBegin ---------------------------------------------------------------
 //When you first tell the ghost to check your email. An intro statement, if you will. Replace with whatever you like.
 
 //im not gonna lie i always struggled setting this up.
 //will replace the lines of course but yknow
OnBIFFBegin
 {
		case vegafront
	{
		when 0; "\0\s[0]Sure,\w8 let's check your mail!\w8 \0\s[7]Please give me a minute..."
		others; "\0\s[23]Sigh.\w8\w8 \0\s[20]Checking email..."		
	}
	--
	"\e"
 }

 
 //---- OnBIFFComplete ---------------------------------------------------------------
 //After they've succeeded in checking your mail, they'll report back with how many new emails you have. Replace the dialogue with whatever would be character appropriate.
 
OnBIFFComplete
 {
	if reference0 == 0 //If you have no new mail.
 	{
		case vegafront
		{
			when 0; "\0\s[0]Seems like there's nothing new!"
			others; "\0\s[20]Nothing new.\w8\w8 There.\w8\w8 Done."
		}
 		//"\0\s[0]There is no new mail.\e"
	}
	elseif reference0 == 1 //If you have one new email.
 	{
		case vegafront
		{
			when 0; "\0\s[1]You've got a single new letter.\w8 \0\s[0]Will you take a look?"
			others; "\0\s[20]One message."
		}
 		//"\0\s[0]There is one new email.\e"
	}
	elseif reference0 > 1 && reference0 < 11 //If you have more than one email but less than eleven.
	{
		case vegafront
		{
			when 0; "\0\s[5]You've got %(reference0) letters!\w8 \0\s[0]Do you want to check them?"
			others; "\0\s[20]%(reference0) messages."
		}
		//"\0\s[0]There are %(reference0) emails.\e"
		//reference0 here stores the amount of new email it finds.
	}
	elseif reference0 > 100 //If there are over one hundred new emails.
	{
		case vegafront
		{
			when 0; "\0\s[3]Ohh...\w8 You've got quite a backlog there.\w8 \0\s[0]\i[5]%(reference0) letters to be exact."
			others; "\0\s[22]...%(reference0) messages.\w8\w8 \0\s[20]\i[7]Messy..."
		}
		//"\0\s[0]There are %(reference0) emails unread.\w8\1\s[10]Woah.\e"
	}
 	else //For any other number of emails. (pretty sure all the options above cover it but just in case yeah)
	{
		case vegafront
		{
			when 0; "\0\s[0]You have %(reference0) letters."
			others; "\0\s[20]%(reference0) messages."
		}
		//"\0\s[0]There are %(reference0) emails.\e"
 	}
	--
	
	//list five last emails you've recieved
	//following code adopted/adapted from zicheq
	if reference7 != "" && reference0 > 0 //added in reference7 here because if there's no headers at all this code is irrelevant lol oops
	{	
		"\n[150]"
		--
		headers = ""
		_display = ""
		_iEnd = 0
		_headSize = ""
		
		if reference7 != "" && reference0 <= 500
		{
			headers = SPLIT(reference.raw[7],C_BYTE1) //the reference.raw here is important, don't make this reference7 unless you've turned off AUTO_DATA_CONVERT
		}

		_headSize = ARRAYSIZE(headers) - 1 //get the amount of headers we got -1, since we start from 0
		--
		//"%(reference0);%(_headSize)\n[150]" //debug?? i guess i just left those in
		--
		if reference0 >= 1 && reference0 <= 5; _iEnd = reference0
		else; _iEnd = 5; //how many letters to show

		for _i = 0; _i < _iEnd; _i++ //show the latest up to 5 letter headers
		{	
			_display += "\![*]"
			_display += SHIORI3FW.EscapeAllTags( headers[_headSize - _i] )
			_display += "\n"
		}
		--
		"\_q\![set,autoscroll,disable]%(_display)"
		--
		ERASEVAR("headers") //This erases the variable so you're not storing all those headers and bloating your save. Also, privacy reasons.
	}	
	--
	"\e"
}

 //---- OnBIFFFailure ---------------------------------------------------------------
 //Various error messages that can happen when your ghost tries to connect to an email account. Replace the dialogue as appropriate.
 
OnBIFFFailure
 {
	if reference0 == "timeout" //If the connection times out.
 	{
		case vegafront
		{
			when 0; "\0\s[3]...Or,\w8 not this time.\w8\n[150]\0\s[0]\i[5]Is everything okay with you network?\w8 \0\s[7]I couldn't access your mailbox in time..."
			others; "\0\s[22]Connection timed out.\w8\w8 \0\s[20]\i[1]Ah well."
		}
 		//"\0\s[0]The connection has timed out.\e"
	}
 	elseif reference0 == "kick" //If the account blocks the ghost from accessing it. I think gmail may do this at first to try and protect your account from unapproved applications using it. It gives you the option to change the setting in the warning email they send you about it.
	{
		case vegafront
		{
			when 0; "\0\s[3]...Orrrr not.\w8 \0\s[0]\i[5]The server won't let me in.\w8 Could you take a look?"
			others; "\0\s[22]\i[8]Rejected..?\w8\w8 \0\s[21]Fair enough."
		}
		//"\0\s[0]We can't access the account.\e"
 	}
	elseif reference0 == "defect" //If the information put into the POP menu is wrong.
 	{
		case vegafront
		{
			when 0; "\0\s[3]Hey,\w8 I don't think you've set this up right.\w8\n[150]\0\s[1]Could you check for me?\w8 \0\s[0]It should be at the POP menu."
			others; "\0\s[23]...Bad configuration.\w8\w8 \0\s[20]\i[7]Fix first.\w8\w8 \0\s[20]POP menu."
		}
		--
		"\![open,configurationdialog]"
 		//"\0\s[0]The email account was not set up properly.\w8\1\s[10]It can be reset at the POP menu.\w8\![open,configurationdialog]\e"
		
		//Note that this ends with \![open,configurationdialog]. This will open up the preferences menu I described above automatically. More on \! functions in the walkthrough. You can delete the \! tag here if you want to.
	}
	elseif reference0 == "artificial"
	{
		case vegafront
		{
			when 0; "\0\s[3]Ah,\w8 have you changed your mind...?\w8\n[150]\0\s[0]\i[5]Well,\w8 \0\s[4]\i[62]um.\i[61]\w8 \0\s[0]\i[5]I'm here if you'd like to take a look later..."
			others; "\0\s[22]Check cancelled.\w8\w8\n[150]\0\s[25]\i[7]Hmph.\w8\w8 \0\s[20]\i[10]\i[7]Make up your mind."
		}
	}
 	else //if there's some other kind of error.
	{
		case vegafront
		{
			when 0; "\0\s[3]...Or,\w8 not,\w8 I suppose.\w8 \0\s[0]\i[5]It just says '%(reference0)'.\w8 \0\s[4]\i[1]I'm not sure what this means..."
			others; "\0\s[21]...Ah.\w8\w8 \0\s[24]'%(reference0)',\w8 it said.\w8\w8 \0\s[20]\i[1]No mail,\w8 though."
		}
		//"\0\s[0]We can't connect to the account.\w8\1\s[10]The error returned is '%(reference[0])'.\e"
		
		//reference0 here stores the error message the email server would have given the ghost, hopefully.
 	}
	--
	"\e"
}


 //**** Network Update *****************************************************************
 //The following functions are for the Network Update of your ghost, where it will connect to your website, check for new files, and update itself. This is a handy feature, but tricky to set up. If you have no intention of using Network Update, you can disregard these, but it won't hurt to fill them out anyway just in case. More details about how to set up Network Update are in the walkthrough (http://www.ashido.com/ukagaka/).
 
 //Replace the dialogue in the next set of functions as appropriate. You can easily test these messages using the Developer panel. Go to Open at the bottom of it and choose one of the four Update Event options to simulate a Ghost's update process. I highly recommend doing this at LEAST once if you're going to use this feature to make sure it flows well.

 
 //---- OnUpdateBegin -------------------------------------------------------------
 //When you first tell your ghost to update. An introductory statement, like OnBIFFBegin above. Replace or add more dialogue as appropriate. Note that they end with \w8 to give a little breather time.
 
 //it would definitely be ideal to try to set up a github repository for updates and such
 //but frankly: no idea how well will that work out
 //too early anyways
OnUpdateBegin
 {
	case vegafront
		{
			when 0
			{ 
				if stage == 0; "\0\s[5]Alright!\w8\n[150]\0\s[0]Let me check for a new version\w8.\w8.\w8." //the other one was a bit uncharacteristic for rigel on stage 0
				else; "\0\s[0]\i[5]\i[8]\i[62]I'm...\i[61]\w8 \0\s[4]not really sure there's anything out there,\w8 \0\s[4]\i[1]but\w8.\w8.\w8."
			}
			others; "\0\s[22]\i[7]An...\w8\w8 \0\s[22]\i[8]\i[10]Update?\w8\w8\n[150]Really?\w8\w8 \0\s[23]Mm\w8.\w8.\w8."
		}
		--
		"\e"
	//"\0\s[0]Starting the update process.\w8\e"
	
	//"\1\s[10]Another message for starting the update process.\w8\0\s[0]Hope you read the directions properly.\w8\e"
 }

 //---- OnUpdateReady ---------------------------------------------------------
 //If the ghost finds new files to update with.
 
OnUpdateReady
 {
 
	reference0++ //makes sure it starts at 1 instead of 0
	case vegafront
		{
			when 0; "\0\s[3]A \i[62]real\i[61] update!?\w8 \0\s[8]I \i[62]need to\i[61] see this!\w8\n[150]\0\s[5]Let's take a look!!"
			others
			{ 
				"\0\s[22]\i[10]...%(reference0) file"
				--
				if reference0 > 1; "s"
				--
				"?\w8\w8 \0\s[24]\i[1]\i[3]Well.\w8\w8 \0\s[20]\i[1]Okay.\w8\w8\n[150]Preparing to download..."
			}
		}
		--
		"\e"
    //"\1\s[10]There are %(reference0) new files.\w8\e"
	
	//reference0 here storing the number of new files. You don't have to use it if you don't want to.
 }


//---- OnUpdateComplete -------------------------------------------------------------
//When the ghost has finished the update process.

OnUpdateComplete
 {
	LegacyShellChange
	--
	if reference0 == "none" //if the ghost had nothing to update with
 	{
 		case vegafront
		{
			when 0;
			{
				if stage == 0; "\0\s[0]\i[5]\i[8]...I'm still on current version?\w8 N-\w8 no worries!\w8\n[150]Any day now,\w8 hah..."
				else; "\0\s[4]\i[1]Just as I thought,\w8 still nothing..."
			} 
			others; "\0\s[23]No updates.\w8\w8 All up to date."
		}	
		--
		"\e"
	}
 	else //if the ghost has updated successfully
	{
		case vegafront
		{
			when 0; "\0\s[8]\i[62]All done!!\i[61]\w8 I...\w8\w8\n[150]\0\s[0]\i[5]\i[8]I don't feel that much different,\w8 actually...\w8\n[150]\0\s[4]\i[1]Maybe we'll find out what's different as we go along."
			others; "\0\s[20]\i[1]Update successful.\w8\w8\n[150]\0\s[24]...Hmm..."
		}	
		--
		FixMenuColors //if the update swapped the menu colors, change them back
 	}
}

 //---- OnUpdateFailure -------------------------------------------------------------
 //Error messages if the update process fails in some way.
 
OnUpdateFailure
 {
	 _file = SPLITPATH(reference1)[2] + SPLITPATH(reference1)[3] //This gets the name and extension of the offending file
	 
	 case vegafront
	{
		when 0 //rigel
		{
			case reference0
			{
				when "timeout"; "\0\s[7]Hmm...\w8 \0\s[1]The server couldn't respond in time.\w8 \0\s[0]\i[5]Maybe one day..."
				when "md5 miss"; "\0\s[7]Something is wrong with %(_file) in particular,\w8 it would seem.\w8 \0\s[4]\i[1]I can't proceed..."
				when "artificial"; "\0\s[0]\i[6]...\i[62]Why\i[61]\0\s[0]\i[5] would you do that?\w8 \0\s[4]\i[5]Did you \0\s[6]\i[62]change your\0\s[4] mind...?\n[150]\w8I'd-\w8\w3 \0\s[0]\i[5]I would \0\s[0]\i[6]\i[62]prefer if\0\s[4]\i[1] you didn't interrupt in the future..."
				when "fileio"; "\0\s[3]Oh..!\w8 \0\s[0]\i[5]%(username),\w8 there isn't enough space for me to download it...\w8 \0\s[4]\i[1]Could you make a little more space for me?"
				when "readonly"; "\0\s[3]Oh...\w8 Um,\w8 %(username),\w8 \0\s[7]I don't think I have the permissions to save anything.\w8\n[150]\0\s[0]You might want to check the folders of the program you're running me on.\w8\n[150]If anything has 'Read-only' ticked on,\w8 \0\s[0]\i[5]could you remove it for me?"
				when "virusdetect"
				{ 
					if stage == 0; "\0\s[3]\i[5]\i[62]Oh!\i[61]\w8 \0\s[3]Oh dear...\w8\n[150]\0\s[0]\i[5]\i[8]I don't like the look of it, %(username),\w8 something seems \i[62]off\i[61] with this..."
					else; "\0\s[3]\i[5]\i[62]Oh!\i[61]\w8 \0\s[3]Oh dear...\w8\n[150]\0\s[0]\i[5]\i[8]I don't like the look of it, %(username),\w8 something seems \i[62]off\i[61] with this...\n[150]\w8\w8\1\t\s[33]\w3\1\s[30]\i[1]\i[14]\0\s[3]\i[5]\i[62]\1Updat\0\s[3]\1e contains\0\s[4]\i[1]\1 malware.\w8\w8 Abort update process\0\s[0]\i[5]\1,\w8 immediately.\0...Well,\w8 \0\s[0]you\1\s[34]\0 heard what he said."
				}
				when "toomanyredirect"; "\0\s[3]The server is just sending me on a wild goose chase!\w8 \0\s[2]\i[7]I feel dizzy..."
				when "executing"; "\0\s[5]We're already downloading!\w8 \0\s[0]No need to start again."
				when "paramerror"; "\0\s[7]Ah,\w8 something's wrong with it...\w8 \0\s[1]These instructions seem strange."
				others; "\0\s[4]Sigh...\w8 '%(reference0)' error.\w8 \0\s[4]\i[1]Maybe another day..."
			}
		}
		others //vega
		{
			"\0\s[23]Error occured.\w8\w8 "
			--
			case reference0
			{
				when "timeout"; "Connection timed out."
				when "md5 miss"; "Checksum mismatch.\w8\w8 \0\s[20]In %(_file),\w8 specifically."
				when "artificial"; "\0\s[25]\i[7]...You.\w8\w8 \i[6]You're the error.\w8\w8 Rude."
				when "fileio"; "\0\s[20]Not enough space."
				when "readonly"; "Read only directory.\w8\w8 \0\s[20]Check your folders."
				when "virusdetect"; "\0\s[25]\i[7]\i[10]Malware detected.\w8\w8 \0\s[23]...Annoying.\w8\w8 \0\s[21]\i[11]Can't even quarantine..."
				when "toomanyredirect"; "Too many redirects."
				when "executing"; "Download in process."
				when "paramerror"; "Parameter error."
				others; "'%(reference0)'."
			}
		}
	}
		--
		"\e"
}



 //---- OnUpdate.OnDownloadBegin ------------------------------------------------
 //This dialogue plays when they begin downloading a new file. WARNING: if you have big plans for your ghost and are going to make a lot of new image files for it (like... i do, cough) at some point, you're going to want to make these messages very brief, if you even have them at all. The reason is because the user will see these messages for EVERY individual file that they have to download, and if you have, say, 50 pna files in the queue, they will see this dialogue 50 times. Not only that, but displaying the dialogue takes some time, so it also extends the download process.
 //If you aren't planning on adding a ton of files at any point, or are only intending to update the .dic files which will be at most a handful of files, then you can go ahead and put a short message in here. But if your ghost is going to grow in leaps and bounds as you work on it, try and keep these messages as brief as possible (or again, just make it "" to have there be no message at all) to make the process that much faster for your user.
 
OnUpdate.OnDownloadBegin
 {
 	// case vegafront
	// 	{
	// 		when 0; "\0\s[5]Downloading file...!"
	// 		others; "\0\s[20]\i[1]Preparing to download..."
	// 	}
	// 	--
	// 	"\e"
	reference1++
	reference2++
	"\0\_qDownloading %(reference0) (%(reference1)/%(reference2))...\w8\w8\e"
 }

 //---- OnUpdate.OnMD5CompareBegin ---------------------------------------------------
 //This checks the hash value of the files in the directory and the files listed in updates.dau, I believe. It will do this for every file the user downloads, so I again remind you of my warning above! For every file they download, they will have to go through three sets of unique dialogue, and it can get old fast. Not to mention it slows things down a lot. Again, I would recommend keeping this very brief, if not just replacing it with "" instead.
 
OnUpdate.OnMD5CompareBegin
 {
	"\e" //yes i think we'll go with the blank
 }

 //---- OnUpdate.OnMD5CompareComplete --------------------------------------------------
 //After they've checked the MD5 value and it matches. See above about the length of this message.
 
OnUpdate.OnMD5CompareComplete
 {
	"\e" //yes i think we'll go with the blank
}

 //---- OnUpdate.OnMD5CompareFailure ---------------------------------------------------
 //If the MD5 values don't match, meaning if the file is missing or damaged or incomplete in some way. If you follow my Network Update directions carefully, this shouldn't come up very often, but don't be surprised if you run into it every now and then by forgetting a file here or there.
 
OnUpdate.OnMD5CompareFailure
 {
	case vegafront
	{
		when 0; "\0\s[7]Something...\w8 doesn't \i[62]match\i[61] here,\w8 I'm afraid...\w8\n[150]\0\s[0]\i[5]\i[8]Something might \i[62]break\i[61] if I download this."
		others; "\0\s[22]Checksum mismatch.\w8\w8 \0\s[24]Can't proceed."
	}
	--
	"\e"
 }

 
 
 //**** Headlines and RSS Feeds *********************************************************
 //This section has to do with the RSS feeds option on your ghost. If you drag and drop a url on them, they'll add it to a list you can access via the right-click menu, and they'll tell you the last set of updates for the site. This works best with RSS feeds I think, but you can experiment with other sites to see how it works.
 //The headlines you see under the tab are in the headline folder under your base SSP install. So it'd be SSP\headlines. You should see the folders for each linked headline in there. I've very rarely ever messed with them or used them. Likewise, your RSS feeds you've added will be under SSP\rss and I don't think that option will appear in the headlines/RSS tab until you add an RSS feed to a ghost. If you want to clear your RSS feeds or headlines, just delete either of those two folders.
 

 //---- OnHeadlinesenseBegin -----------------------------------------------------------
 //This dialogue comes up when you select something in your RSS feed as an intro, like the other On[Thing]Begin things we've seen so far. Replace as you need.
 
OnHeadlinesenseBegin
 {
	case vegafront
	{
		when 0; "\0\s[5]Alright!\w8 \0\s[0]Let me check %(reference0)."
		others; "\0\s[22]'%(reference0)'. \0\s[20]Okay."
	}
	//"\0\s[0]You've selected %(reference0).\e"
 }

 //---- OnHeadlinesense.OnFind --------------------------------------------------------
 //This actually displays the feed. My set-up for it is pretty simple but gets the job done, it just has the name of the feed, a line space, then a list of links. You shouldn't have to edit any of these and I probably wouldn't honestly.
 
OnHeadlinesense.OnFind
{
	case vegafront
	{
		when 0; "\0\s[0]"
		others; "\0\s[20]"
	}
	//----  What page You're on
 	if reference2 == "First" || reference2 == "First and Last"
	{
 		//---- First page
		"\b2%(reference0):"
 	}
	elseif reference2 == "Next"
 	{
		//---- Second page
 		"\b2%(reference0):"
	}
 	elseif reference2 == "Last"
	{
 		//---- Last page
		"\b2%(reference0):"
 	}
	--
 	//---- Headlines
	"\n\n%reference3\n"
 	--
	//---- Pages
 	if reference2 == "First" || reference2 == "Next"
	{
 		//---- Bottom buttons
		"\![*]\q[Next,] - \![*]\q[Stop,Headline_Cancel]\e"
 	}
	else
 	{
		//---- Bottom buttons
 		"\![*]\q[Stop,Headline_Cancel]\e"
 	}
	
}


Select.Headline_Cancel
{
	case vegafront
	{
		when 0; "\0\s[1]Ah,\w8 \0\s[0]all done?"
		others; "\0\s[20]\i[1]Alright."
	}	
	//"\0\s[0]Headline viewing complete.\e"
	//Actually I take that back, you can change this line of dialogue here for when you're done looking at a headline to something else if you want.
}

 //---- OnHeadlinesenseComplete -------------------------------------------------------
 //This is an odd function. As far as I can gather, this is if the headline information gathering was successful, but there's no new information to report. Possibly if none of the feeds have updated? I'm a little fuzzy on this one.
 
OnHeadlinesenseComplete
 {
	case vegafront
	{
		when 0; "\0\s[7]Doesn't seem like there was anything new..."
		others; "\0\s[21]Nothing new."
	}	
	//"\0\s[0]No update. %(reference0).\e"
	//reference0 here supposedly always stores the value no update? Again this isn't hugely clear from what I can see. Anyway, this dialogue has never come up for me while using a ghost, so perhaps someone else who runs into it can offer more clarity. For now, you can leave it as is, or write up something else in here if you like.
 }

 //---- OnHeadlinesenseFailure ---------------------------------------------------------
 //This one is more clearly for when the ghost can't get the information it needs about the RSS feed in question.
 
OnHeadlinesenseFailure
  {
	case vegafront
	{
		when 0 //rigel
		{
			"\0\s[7]Hmm...\w8 " //This sets up an exchange between the two characters. The final part of the exchange continues below in the if statements.
			--
			if reference0 == "can't download" //if they can't download the rss feed for some reason
			{
				"\0\s[0]\i[5]I can't download this...\w8 Maybe another time."
			}
			elseif reference0 == "can't analyze" //if they can't read the file for some reason
			{
				"\0\s[0]\i[5]Sorry,\w8 this is unreadable to me."
			}
			else //other errors
			{
				"\0\s[0]\i[5]I'm not sure why,\w8 but I can't access this...\w8 \0\s[4]\i[1]Sorry."
			}
		}
		others //vega
		{
			"\0\s[24]Mm.\w8\w8 Can't check the feed.\w8\w8 " //This sets up an exchange between the two characters. The final part of the exchange continues below in the if statements.
			--
			if reference0 == "can't download" //if they can't download the rss feed for some reason
			{
				"\0\s[22]Couldn't download."
			}
			elseif reference0 == "can't analyze" //if they can't read the file for some reason
			{
				"\0\s[21]Can't read it."
			}
			else //other errors
			{
				"\0\s[23]Something broke."
			}
		}
		--
		"\e"
	}
	
	--
 	"\e" //And here is the final /e, so you don't need to include it in the if statements above. Another one of the very rare cases this occurs.
}


//**** Ghost Creation and Updating ******************************************************
//These next few are related to creating updates2.dau and .nar files for your ghosts. You'll want to at least make a .nar to distribute your ghost, and if you're going to be using Network Update, you'll also want to make updates2.dau as well. It's unlikely that anyone other than you will see these messages, but you should be thorough and replace them anyway.
//Primarily, you will see these messages when you're creating your updates file or updating your ghost.nar file. More on this on the walkthrough page. However, you can drag and drop another ghost's folder onto them and they'll also create a nar for that as well, if for some reason you want to do that.


 //---- OnUpdatedataCreating -----------------------------------------------------------
 //This will begin the creation of the updates2.dau file. Not unlike the other beginning functions you've seen throughout this file. Replace with whatever you like.
 
OnUpdatedataCreating
 {
	case vegafront
	{
		when 0; "\0\s[0]Alright,\w8 this should give you an updates2.dau file..."
		others; "\0\s[20]Creating updates2.dau..."
		--
		"\e"
	}
	//"\1\s[10]Creating the updates2.dau file.\e"
 }

 //---- OnUpdatedataCreated -------------------------------------------------------------
 //After the updates2.dau file has been successfully created. Replace with whatever.
 
OnUpdatedataCreated
 {
	case vegafront
	{
		when 0; "\0\s[5]There!\w8 \0\s[0]Now,\w8 \0\s[0]\i[5]I'm not quite sure what this is for...\w8 I only have instructions on how to make this.\w8\n[150]\0\s[0]You probably know how to use this,\w8 don't you?"
		others; "\0\s[20]Done.\w8\w8 \0\s[22]I guess.\w8\w8 \0\s[20]Use as you see fit."
		--
		"\e"
	}
	//"\0\s[0]Successfully created updates2.dau.\e"
 }

 //---- OnNarCreating ------------------------------------------------------------------
 //Along the same lines, this is when they begin creating a .nar file for your ghost. .nars are the format most ghosts will be in, something similar to a .zip file, and this is the format you'll be distributing your ghost in most likely. Replace the dialogue with whatever you like.
 
OnNarCreating
 {
	case vegafront
	{
		when 0; "\0\s[5]Let me just wrap this in a neat little package for you."
		others; "\0\s[20]Creating a .nar package."
		--
		"\e"
	}
	//"\0\s[0]Creating the .nar file.\e"
 }

 //---- OnNarCreated -------------------------------------------------------------------
 //After the nar file has been successfully created.
 
OnNarCreated
 {
	case vegafront
	{
		when 0; "\0\s[8]Ta-da~!\w8\n[150]\0\s[0]This seems to be just like any archive.\w8\n[150]\0\s[5]It's ready to be shared with the world!"
		others; "\0\s[20]\i[1]Done.\w8\w8 Created %(refrerence0)."
		--
		"\e"
	}
	//"\0\s[0]Successfully created '%(reference0)'.\e"
	//reference0 here will be the filename of the nar you just created. It should drop the nar in the folder where all your ghosts are listed, ssp\ghost\.
 }

 //****　Clock Synchronization　**********************************************************
//**** 미카 (http://homepage2.nifty.com/ko-ki/)의 사전을 참고했습니다．감사＞こ 나무 성
//This set of functions was done by the above Korean coder, I think. I'll leave their comment there untouched. 
//If you right click your ghost and go into the Utilities menu, there's an option called NTP Time Sync. That's what this is for!
 

 //----OnSNTPBegin ------------------------------------------------------------------
 //As with the others you've seen above, this is the beginning bit of dialogue for this process.
 
OnSNTPBegin
 {
	case vegafront
	{
		when 0; "\0\s[0]Let's check the time,\w8 why don't we?\w8\w8"
		others; "\0\s[20]Synchronizing the clock...\w8\w8"
		--
		"\e"
	}
	//"\0\s[0]We're going to check the time.\w9\1\s[10]We are connecting to %(reference0).\e"
	//%(reference0) here is the site they'll be using to check your clock. I wouldn't worry too much about what the site is or where it's defined, it should be accurate. Replace the dialogue with what you need, you don't have to use reference0 if you don't want to.
 }


 //----　OnSNTPCompare----------------------------------------------------------
 //Now the ghost is going to compare the current time of your computer's system to the time of the site they connected to. Most of this you don't have to touch, just replace the dialogue as indicated.
 
OnSNTPCompare
 {
 	"Retrieved Time: %reference1[1]/%reference1[2]/%reference1[0] %reference1[3]:%reference1[4]:%reference1[5]\w2\n/
	Your Time: %reference2[1]/%reference2[2]/%reference2[0] %reference2[3]:%reference2[4]:%reference2[5]\w2\n/
 	\n"
	--
 	if reference3 == 0 //if your clock is already accurate.
	{
		case vegafront
		{
			when 0; "\0\s[5]Seems like everything's in working order.\w8\0\s[0] No need to correct your clock!"
			others; "\0\s[20]\i[1]All good.\w8\w8 No synchronization required."
		}
 		//"\0\s[0]Your clock is accurate.\e" //replace this with whatever message you like.
	}
 	else //if your clock doesn't match
	{
	//you can redo this top line up to the \n\n[half] part, but leave the \q options alone.
 		//"\0\s[0]Your clock doesn't match.\w6\nShould we fix it?"
		case vegafront
		{
			when 0; "\0\s[0]\i[5]Ah,\w8 I see our clock is a little off.\w8 \0\s[0]May I synchronize it for you?"
			others; "\0\s[24]Time doesn't match.\w8\w8 \0\s[22]\i[1]May I?"
		}
		--
		"\n[150]\_q\![*]\q[Fix it,OnSNTPEXECUTE]\n/
 		\![*]\q[Leave it alone,OnSNTPCANCEL]\e"
	}
	--
	"\e"
 }

 //----　OnSNTPEXECUTE ------------------------------------------------------------------
 //After they've fixed your clock. Note the \6 tag - that's what actually changes the clock. Do not remove that tag. Replace the rest with what you want.
 
OnSNTPEXECUTE
 {
	case vegafront //it's possible that on older systems the executable doesn't need permission. i might have to test against the likes of xp, vista, and 7. if that's the case i can easily make this vary per windows version
	{
		when 0; "\0\s[5]Okay!\w8\n[150]\0\s[0]Now,\w8 \0\s[1]you'll probably get a big pop-up in a moment!\w8 \0\s[5]It's just me,\w8 don't worry!\w8\n[150]I have to ask for your permission first is all.\w8\w8"
		others; "\0\s[24]\i[2]Mm.\w8\w8 \0\s[20]\i[8]Might need permission.\w8\w8\n[150]\0\s[20]\i[1]SSPH.exe is me.\w8\w8"
	}
	--
	"\6\e"
	//"\0\s[0]The clock has been fixed.\6\e"
 }

 //----　OnSNTPCANCEL ------------------------------------------------------------------
 //If you decide not to change your clock when they tell you it doesn't match. Replace with what you want.
 
OnSNTPCANCEL
 {
	case vegafront
	{
		when 0; "\0\s[3]Oh, alright!\w8 \0\s[0]\i[5]I'm sure you know what you're doing!"
		others; "\0\s[22]Really?\w8\w8 \0\s[20]\i[8]Okay."
	}	
	--
	"\e"
 }

 //----　OnSNTPFailure ------------------------------------------------------------------
 //If they can't change the clock for some reason, possibly because the site timed out but it could be anything. Replace with what you want.
 
OnSNTPFailure
 {		
	case vegafront
	{
		when 0; "\0\s[3]Ah...\w8 \0\s[0]\i[5]\i[8]Sorry,\w8 I can't seem to get the time.\w8 \0\s[4]\i[1]Something went off the rails there."
		others; "\0\s[20]\i[8]...Mm.\w8\w8 \0\s[24]Something's wrong.\w8\w8 \0\s[23]Oh well."
	}	
	--
	"\e"
	//"\0\s[0]The clock wasn't changed.\w8\1\s[10]Something went wrong.\e"
 }


 //--OnDisplayChange----------------------------------------------------------------
 //This is when you change the resolution of your monitor while your ghost is running. Don't mess with any of this, it should be fine as is.

 //this event also gets the info upon booting. however the ghost won't actually Say anything in response even if the event says so. that's called a Notify event
 //it Will however respond if the resolution changed during run, like the og comment says
OnDisplayChange
 {
	displaybpp    = reference0
 	displaywidth  = reference1
	displayheight = reference2
 }



 //---- OnScreenSaverStart -------------------------------------------------------
 //This happens if your ghost is running when your screensaver starts up. I've never got this to trigger but I love this idea, and I feel like there's a way to do it I haven't figured out yet. So you can skip these for now if you want, but if I do ever get it working, I'll come back and make a note of it.

// this possibly hasn't worked because this was a bug since 2021. this works on windows 11 tho
 
OnScreenSaverStart
 {
	screensaver = 1
	case vegafront
	{
		when 0; "\0\s[8]Ooh..."
		others;	"\0\s[26]\i[0]Mm..."
	}
	--
	"\e"
	//"\0\s[0]The screensaver is starting.\w7\1\s[10]Wiggle your mouse!\e"
	//Replace with what you want.
 }

 //---- OnScreenSaverEnd -------------------------------------------------------
 //When you wiggle your mouse and stop your screensaver.
 
OnScreenSaverEnd
 {
	"%(welcomeback)" //i'm. getting lazy
 }


//my impression is that the external events need to Send their message To the ghost in order for it to respond to the likes of
//OnApplication events. no wonder they don't work, would've been VERY COOL THOUGH i wouldn't need to make the windowcheck beast


 //---- OnBatteryLow -----------------------------------------------------------------
 //This is for when your laptop's battery is low. I don't have a laptop, so I've never really been able to test this. Perhaps someone with a laptop can report back to me with more details about the battery related functions?
 
 //i do use a laptop but i practically never plug it out. who knows!
OnBatteryLow
 {
	case vegafront
	{
		when 0; "\0\s[0]\i[5]...Hey,\w8 %(username),\w8 could you please plug in the power?\w8 \0\s[4]\i[1]Your battery has %(reference0) left."
		others; "\0\s[20]\i[8]%(username).\w8\w8 Battery low.\w8\w8 \0\s[22]Be careful."
	}
	--
	"\e"
	//"\1\s[10]The laptop's battery is low. \w8\0\s[0]There is %(reference0) power left.\e"
	//reference0 here is the amount of battery power. Replace as you like.
 }
 
 //--OnBatteryCritical-----------------------------------------------------------------
 //Likewise, this is for if your battery is REALLY low. Replace as you like.
 
OnBatteryCritical
{
	case vegafront
	{
		when 0; "\0\s[0]\i[5]%(username)...?\w8 \i[62]Please\i[61] plug in your laptop,\w8 it'll shut down any moment now..."
		others; "\0\s[21]%(username).\w8\w8 Battery critical.\w8\w8 \0\s[20]\i[7]Plug in the charger."
	}
	--
	"\e"
	//"\0\s[0]The laptop's battery is critically low.\e"
}
 
OnShellScaling
{
	scale = reference0 //will be needed for OnCorrectKero in anchor.dic
	case vegafront
	{
		when 0 //rigel
		{
			if reference0 >= 150 //this number is the percentage that you're scaling the ghost up to. In this case, it's 150%.
			{
				"\0\s[0]\i[5]Getting a little more cramped in here,\w8 isn't it...?\e"
			}
			elseif reference0 == 100 //this is 100%
			{
				"\0\s[5]Ahh...\w8 Back to normal.\w8 \0\s[0]Thank you.\e"
			}
			elseif reference0 <= 50 //this is 50%. You can add any percentage that's listed under shell scaling if you want using more elseifs.
			{
				"\0\s[3]Wow...\w8 \0\s[8]I'll bet my voice could really echo through the desktop now...\w8\w8\w8"
				//if there is a link to balloon scaling it should get followed up by balloon scaling i htink
			}
		}
		others //vega
		{
			if reference0 >= 150 //this number is the percentage that you're scaling the ghost up to. In this case, it's 150%.
			{
				"\0\s[21]\i[1]...Feeling powerful.\w8\w8 \0\s[20]\i[8]Still can't do much.\w8\w8\e"
			}
			elseif reference0 == 100 //this is 100%
			{
				"\0\s[20]\i[1]Feels familiar.\e"
			}
			elseif reference0 <= 50 //this is 50%. You can add any percentage that's listed under shell scaling if you want using more elseifs.
			{
				"\0\s[28]Small...\w8\w8\w8"
			}
		}
	}	
}

//i didn't find a way to Check whether balloon scale link to the shell is enabled, but if there is it would be nice to make here a nod like hey. you can have me small and not have a font for ants actually ehre's how you do it
OnBalloonScaling
{
	case vegafront
	{
		when 0 //rigel
		{
			if reference0 <= 50 //this is 50%. You can add any percentage that's listed under shell scaling if you want using more elseifs.
			{
				"\0...\0\s[3]Um.\w8 \0\s[0]\i[5]Can you hear me,\w8 actually?\n[150]This font is quite small, isn't it...?\e"
			}
		}
		others //vega
		{
			if reference0 <= 50 //this is 50%. You can add any percentage that's listed under shell scaling if you want using more elseifs.
			{
				"\0\s[20]\i[8]Possibly inaccessible...\w8\w8 \0\s[22]Hope you can hear me.\e"
			}
		}
	}	
}

//just screwing around. if tama allows you to do that why not right?
//i mean the dialogue gives it away but don't worry ur pretty head about it anyway mwah
On_tamaOpen{
	SETTAMAHWND(reference[0])
	case vegafront
	{
		when 0
		{
			SHIORI3FW.Push_X_SSTP_PassThru('Title','Watching Aster: Module Altair')
			SHIORI3FW.Push_X_SSTP_PassThru('border.color','8E2C2C')
			SHIORI3FW.Push_X_SSTP_PassThru('background.color','D9FFD6')
			SHIORI3FW.Push_X_SSTP_PassThru('default.color','200203')
			--
			"\0\s[3]D-\w8 Debugging?\w8 ...\s[0]\i[6]I'd prefer it didn't call me that..."
			"\0\s[0]Good luck!\w8 \i[5]Don't break anything in there, will you..?"
			"\0\s[8]What are we learning today?"
		}
		when 1
		{
			SHIORI3FW.Push_X_SSTP_PassThru('Title','Watching Aster: Module Vega')
			SHIORI3FW.Push_X_SSTP_PassThru('border.color','2E4291')
			SHIORI3FW.Push_X_SSTP_PassThru('background.color','F1DBDD')
			SHIORI3FW.Push_X_SSTP_PassThru('default.color','040434')
			--
			"\0\s[20]Debug...\w8\w8 \i[1]Be careful."
			"\0\s[22]Tama.\w8\w8 Curious."
			"\0\s[26]Mm...\w8\w8 Let me know.\w8\w8 Once done."
		}
	}
	--
	"\e"
}
On_tamaExit{
	SETTAMAHWND(0)
	case vegafront
	{
		when 0
		{
			"\0\s[5]Okay!\w8 Back to work."
			"\0\s[8]Ooh,\w8 done already?"
			"\0\s[19]Well,\w8 that was relatively painless!"
		}
		when 1
		{
			"\0\s[20]\i[1]All done?"
			"\0\s[20]\i[1]Goodbye,\w8\w8 Tama."
			"\0\s[20]Debug done...\w8\w8 \i[1]No longer watched."
		}
	}
	--
	"\e"
}

OnDarkTheme //reaction to light/dark theming being switched up on windows 10/11. no mixed modes because switching between Light and Dark in windows 11 FOR SOME REASON doesn't switch both of these variables simultaneously, making aster interrupt himself. thanks microsoft, probably
{
	"\w8\w8\w8" //slight pause for the color scheme to finish switching
	--
    if reference0 == 1 && reference1 == 1 //full dark mode
    {
		case vegafront
		{
			when 0
			{
				"\0\s[0]Switching things up?"
				"\0\s[5]Ooh,\w8 dark mode!\w8 \0\s[0]Not my favorite,\w8 but it's not a bad look,\w8 is it?"
				"\0\s[0]Feeling dark colors today,\w8 are you?\w8 Alright!"
				if stage == 3; "\0\s[3]So dark all of a sudden...!\w8 Oh-\w8 %(username),\w8 \0\s[4]\i[1]could you give me a minute?\w8\n[150]\0\s[0]\i[5]\i[8]I feel just a notch dizzy...\w8 \0\s[2]I think I am being pushed out-\![raise,OnSwitch]"
			}
			others;
			{
				"\0\s[20]\i[1]Nice and calm..."
				"\0\s[20]\i[1]Nice and dark..."
				"\0\s[22]\i[1]Feels better.\w8\w8 Feels like home."
			}
		}	
    }
	elseif reference0 == 0 && reference1 == 0 //full light mode
	{
		case vegafront
		{
			when 0
			{
				{
					"\0\s[8]Goooood morning,\w8 %(username)!"
					--
					if timeslot != "morning" && timeslot != "earlymorning"; "\w8\n[150]\0\s[3]Well,\w8 \0\s[0]\i[5]it's not morning,\w8 \0\s[19]but you know what I mean!\0\s[0]"
				}
				"\0\s[8]Suuuunriiiise~!"
				"\0\s[8]Ahh,\w8 \0\s[0]much better,\w8 don't you think?"				
			}
			others;
			{
				"\0\s[20]\i[7]\i[10]Mm.\w8\w8 \0\s[20]\i[8]Too bright."
				"\0\s[23]Could you...\w8\w8 \0\s[20]\i[8]Turn down.\w8\w8 \0\s[23]Bright."
				"\0\s[23]Mmh.\w8\w8 \0\s[20]\i[8]Fine."
				if stage == 3; "\0\s[23]Gh.\w8\w8 \0\s[21]Too-\w8 \0\s[20]\i[7]\i[10]Too bri-\![raise,OnSwitch]"
			}
		}	
	}
	--
	//"%(reference0); %(reference1)"
	"\e"
    
}

//capitalize first letter of the word and return the resulting string
//mostly for pronouns.
OnCapitalize
{
	TOUPPER( SUBSTR( _argv[0],0,1 ) ) + TOLOWER( SUBSTR( _argv[0],1,( STRLEN( _argv[0] ) - 1 ) ) )
}
